/* Author: David */
IMPLEMENTATION
    wd_i
REFINES
    wd
VALUES
    timeout=50
IMPORTS
    counter
INVARIANT
    overflow = FALSE &
    timeout - value = ticker
    
INITIALISATION
   VAR count IN
       count := 0;
       WHILE count < timeout DO
           inc;
           count := count+1
       INVARIANT
           value = count
       VARIANT
           timeout - count
       END
   END
OPERATIONS
   start = zero;

   tick =
   VAR elapsed, diff IN
       elapsed <-- get;
       diff := timeout - elapsed;
       IF diff > 0 THEN
           inc
       END
   END;
   
   res <-- expired =
   VAR elapsed, diff IN
       elapsed <-- get;
       diff := timeout - elapsed;
       IF diff = 0 THEN
           res := TRUE
       ELSE
           res := FALSE
       END
   END
END
